#!/usr/bin/perl
#
#  - Check for /dev/actisense*
#  - While /dev/actisense* exists, keep n2kd running. If it quits, restart it.
#  - Also keep n2k.php -monitor running. If it quits, restart it.
#  - Stop n2kd if /dev/actisense disappears.
#
# This assumes there is a configuration file /etc/default/n2kd
# containing one or more of the following configuration settings:
#
#       ACTISENSE_PRIMARY=/dev/actisense-1
#       ACTISENSE_SECONDARY=/dev/actisense-2
#       MONITOR=false
#       ANALYZER_OPTIONS=
#       N2KD_OPTIONS=
#
# Leave out ACTISENSE_SECONDARY if you have only one Actisense gateway.
# Leave MONITOR set to false for now; its contents have not been open sourced yet.
#
# (C) 2009-2014, Kees Verruijt, Harlingen, The Netherlands.
#
# This file is part of CANboat.
#
# CANboat is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# CANboat is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with CANboat.  If not, see <http://www.gnu.org/licenses/>.
#

use ConfigReader::Simple;

my $configFile = '/etc/default/n2kd';
my $config = ConfigReader::Simple->new($configFile);

die "Could not read config! $ConfigReader::Simple::ERROR\n" unless ref $config;

my $ACTISENSE_PRIMARY = $config->get('ACTISENSE_PRIMARY');
my $ACTISENSE_SECONDARY = $config->get('ACTISENSE_SECONDARY');
my $MONITOR = $config->get('MONITOR');
my $N2KD_OPTIONS = $config->get('N2KD_OPTIONS');
my $ANALYZER_OPTIONS = $config->get('ANALYZER_OPTIONS');
die "Configuration file $configFile incomplete: No ACTISENSE_PRIMARY" unless $ACTISENSE_PRIMARY;

my $LOGFILE = '/var/log/n2kd_monitor.log';
my $N2KD_LOGFILE = '/var/log/n2kd.log';
my $MONITOR_LOGFILE = '/var/log/n2k-status.log';

my $stat;
my $n2kd;
my $monitor;
my $child;
my $stop = 0;
my $last_monitor = 0;

if ($MONITOR ne "true" && $MONITOR ne "yes")
{
  # Disable the monitoring part. This is not open source yet, so disable it by default.
  $last_monitor = LONG_MAX;
}

use POSIX();

sub logText($)
{
  my ($t) = @_;

  print POSIX::strftime('%Y-%m-%d %T: ', localtime) . $t . "\n";
}

sub daemonize()
{
  chdir '/';
  open STDIN, '/dev/null' or die "Can't read /dev/null: $!";
  open STDOUT, '>>', $LOGFILE or die "Can't write $LOGFILE: $!";
  defined(my $pid = fork) or die "Can't fork: $!";
  exit if $pid;
  die "Can't start a new session: $!" if setsid == -1;
  open STDERR, '>&STDOUT' or die "Can't dup stdout: $!";
}

sub sigHandler()
{
  logText("Got signal to quit.\n");
  $stop = 1;
}

daemonize();

logText("Delayed start of N2KD monitor.");
sleep 10;
logText("Starting N2KD monitor.");

$SIG{'INT'} = 'sigHandler';
$SIG{'HUP'} = 'sigHandler';

if (!stat($ACTISENSE_PRIMARY))
{
  logText("Waiting for $ACTISENSE_PRIMARY to appear.");
}

if (!pipe(PIPEREAD, PIPEWRITE))
{
  die "Cannot create pipes\n";
}

for (;;)
{
  while (($child = POSIX::waitpid(-1, POSIX::WNOHANG)) > 0)
  {
    if ($child == $n2kd)
    {
      logText "N2KD monitor port daemon $child finished.";
      $n2kd = undef;
    }
    elsif ($child == $monitor)
    {
      $monitor = undef;
    }
  }

  if ($stop == 0 and stat($ACTISENSE_PRIMARY))
  {
    if (!$stat)
    {
      logText("Hardware device $ACTISENSE_PRIMARY found.");
      $stat = 1;
    }
    if (!$n2kd)
    {
      if (($n2kd = fork()) == 0)
      {
        open STDIN, '<&PIPEREAD' or die "Can't read PIPEREAD: $!";
        open STDOUT, '>&PIPEWRITE' or die "Can't reassign PIPEWRITE: $!";
        open STDERR, '>>', $N2KD_LOGFILE or die "Can't write to $N2KD_LOGFILE $!";
        $ENV{'PATH'} = '/usr/local/bin:/bin:/usr/bin';
        if ($ACTISENSE_SECONDARY)
        {
          exec '/bin/bash', '-c', "actisense-serial -t 10 $ACTISENSE_PRIMARY | actisense-serial $ACTISENSE_SECONDARY -t 10 | analyzer $ANALYZER_OPTIONS -json | n2kd $N2KD_OPTIONS";
        }
        else
        {
          exec '/bin/bash', '-c', "actisense-serial -t 10 $ACTISENSE_PRIMARY | analyzer $ANALYZER_OPTIONS -json | n2kd $N2KD_OPTIONS";
        }
      }
      elsif ($n2kd)
      {
        logText("Starting N2K daemon $n2kd.");
      }
      else
      {
        logText("Fork failed.");
      }
      sleep(15);
    }
    if (!$monitor && (time > $last_monitor + 30))
    {
      $last_monitor = time;
      if (($monitor = fork()) == 0)
      {
        open STDIN, '/dev/null' or die "Can't read /dev/null: $!";
        open STDOUT, '>>', $MONITOR_LOGFILE or die "Can't write to $MONITOR_LOGFILE $!";
        open STDERR, '>&STDOUT' or die "Can't dup stdout: $!";
        exec 'php5', '/usr/local/bin/n2k.php', '-monitor';
      }
      if (!$monitor)
      {
        logText("Fork monitor failed.");
      }
    }
  }
  else
  {
    if ($stop == 0 and $stat)
    {
      logText("Hardware device $ACTISENSE_PRIMARY disappeared.");
      $stat = undef;
    }
    if ($n2kd)
    {
      logText("Requesting stop for N2K port daemon $n2kd.");
      kill 2, $n2kd;
      system 'killall -9 actisense-serial';
    }
    if ($stop)
    {
      exit(0);
    }
  }
  sleep(5);
}
